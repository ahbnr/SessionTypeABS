plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'application'
    id 'antlr'
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

group 'de.ahbnr'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation('org.junit.jupiter:junit-jupiter:5.5.0')
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.5.0')
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.5.0")
    testImplementation("org.assertj:assertj-core:3.12.0")
    testImplementation("com.zaxxer:nuprocess:1.2.3")
    testImplementation("com.pholser:junit-quickcheck:0.9")
    testImplementation 'com.pholser:junit-quickcheck-core:0.9'
    testImplementation 'com.pholser:junit-quickcheck-generators:0.9'

    testRuntime ('org.junit.jupiter:junit-jupiter-engine:5.5.0')
    test.useJUnitPlatform() // fix "test events not received" bug in IDEA

    //implementation project('abstools')
    implementation files('../abstools/frontend/dist/absfrontend.jar')

    implementation 'info.picocli:picocli:4.0.0-beta-2'

    implementation group: 'commons-io', name: 'commons-io', version: '2.6'

    implementation "com.github.adelnizamutdinov:kotlin-either:2.0.1"

    // set antlr version
    antlr "org.antlr:antlr4:4.7"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

application {
    mainClassName = 'de.ahbnr.sessiontypeabs.cli.CLI'
}

jar {
    manifest {
        attributes 'Main-Class': 'de.ahbnr.sessiontypeabs.cli.CLI'
    }
}

shadowJar {
  minimize()
}

run {
    if (project.hasProperty('runArgs')) {
        args findProperty('runArgs')
    }
}

test {
    useJUnitPlatform()
    testLogging {
		events "passed", "skipped", "failed"
	}
}

// Make ANTLR generated sources available
sourceSets {
  main.java.srcDirs += "${project.buildDir}/generated-src/antlr/main"
}

generateGrammarSource {
  arguments += ["-visitor", "-package", "de.ahbnr.sessiontypeabs.antlr"]
  outputDirectory = file("${project.buildDir}/generated-src/antlr/main/de/ahbnr/sessiontypeabs/antlr")
}

compileKotlin.dependsOn generateGrammarSource